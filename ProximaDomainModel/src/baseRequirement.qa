System proxima

Dispatch identificationRequest : identificationRequest
Dispatch identificationResponse : identificationResponse(I)
Dispatch dataRequest : dataRequest(DOCTOR_ID, PATIENT_ID)
Dispatch dataResponse : dataResponse(PATIENT_ID, NAME, SURNAME, BLOOD_GROUP)

Event local_start : start

Context ctxBaseDebug ip[host="localhost" port=1406 ]
//Context ctxPatient ip[host="localhost" port=1406] -g white
//Context ctxDoctor ip[host="localhost" port=6041] -g blue
//Context ctxControl ip[host="localhost" port=6042] -g yellow

QActor qpatient context ctxBaseDebug {
	Rules {
		identifier(patient1).
	}
	
	Plan init normal
	[
		println("[qpatient] started!")
		//[?? identifier(K)] actorOp createIdentifier(K)
	]
	switchTo waitForRequest
	
	Plan waitForRequest 
	[
		println("[qpatient] waiting for request")
	]
	transition stopAfter 10000
			   whenMsg identificationRequest -> handleIdentificationRequest
			   finally repeatPlan
	
	Plan handleIdentificationRequest resumeLastPlan
	[
		println("[qpatient] handling identification request");
		[!? identifier(K)] replyToCaller -m identificationResponse : identificationResponse(K)
		/*actorOp getIdentifier;
		[?? actorOpDone(OP, R)] replyToCaller -m 
							identificationResponse : identificationResponse(R)*/
	]
}

QActor qpatient2 context ctxBaseDebug {
	Rules {
		identifier(patient2).
	}
	
	Plan init normal
	[
		println("[qpatient2] started!")
		//[?? identifier(K)] actorOp createIdentifier(K)
	]
	switchTo waitForRequest
	
	Plan waitForRequest 
	[
		println("[qpatient2] waiting for request")
	]
	transition stopAfter 10000
			   whenMsg identificationRequest -> handleIdentificationRequest
			   finally repeatPlan
	
	Plan handleIdentificationRequest resumeLastPlan
	[
		println("[qpatient2] handling identification request");
		[!? identifier(K)] replyToCaller -m identificationResponse : identificationResponse(K)
		/*actorOp getIdentifier;
		[?? actorOpDone(OP, R)] replyToCaller -m 
							identificationResponse : identificationResponse(R)*/
	]
}


QActor qdoctor context ctxBaseDebug {
	Rules {
		identifier(doctor1).
		distance(qpatient, 10).
		distance(qpatient2, 70).
	}
	
	Plan init normal
	[
		println("[qdoctor] started!");
		demo consult("./src/it/unibo/qatheory/qDoctorTheory.pl");
		actorOp buildCustomGui("doctorGui")
		//[?? identifier(K)] actorOp createIdentifier(K)
	]
	switchTo idle
	
	Plan idle 
	[
		println("[qdoctor] waiting for emergency")	
	]
	transition stopAfter 30000
			   whenEvent local_start -> requestPatientIdentifier //readIdentifier
	
	/*Plan readIdentifier
	[
		actorOp getIdentifier;
		println("[qdoctor] identifier read");
		[?? actorOpDone(OP, R)] addRule identifier(R)
	]
	switchTo requestPatientIdentifier*/
	
	Plan requestPatientIdentifier 
	[
		println("[qdoctor] sending identification request to patient");
		[!? findNearest(NAME, _)] forward NAME -m identificationRequest : identificationRequest
	]
	transition stopAfter 2000
			   whenMsg identificationResponse -> handlePatientIdentifierResponse
			   
    Plan handlePatientIdentifierResponse
    [
    	println("[qdoctor] patient identifier read");
    	onMsg identificationResponse : identificationResponse(PATIENT_ID) ->
			addRule patientid(PATIENT_ID)
    ] 
    switchTo sendDataRequest
    
    Plan sendDataRequest
    [
    	println("[qdoctor] sending data request to qcontrol");
    	[!? identifiers(DOCTOR_ID, PATIENT_ID)] 
			forward qcontrol -m dataRequest : dataRequest(DOCTOR_ID, PATIENT_ID)
    ]
    transition stopAfter 2000
    	whenMsg dataResponse -> handleDataResponse
    
    Plan handleDataResponse
    [
    	println("[qdoctor] patient data retrieved");
    	onMsg dataResponse : dataResponse(PATIENT_ID, NAME, SURNAME, BLOOD_GROUP) -> 
    		addRule dataResponse(PATIENT_ID, NAME, SURNAME, BLOOD_GROUP)
    ] switchTo showPatientData
    
    Plan showPatientData
    [
    	[!? dataResponse(_, NAME, SURNAME, BLOOD_GROUP)] actorOp showDataOnGUI(NAME, SURNAME, BLOOD_GROUP)
    ] 
    switchTo idle
}

QActor qcontrol context ctxBaseDebug {
	Rules {
		data(patient2, jane, doe, a_positive).
		data(patient1, john, doe, ab_negative).
	}
	
	Plan init normal 
	[
		demo consult("./src/it/unibo/qatheory/qControlBaseTheory.pl");
		println("[qcontrol] started!")
	]
	switchTo waitForRequest
	
	Plan waitForRequest
	[
		println("[qcontrol] waiting for request")
	]
	transition stopAfter 10000
			   whenMsg dataRequest -> handleRequest
			   finally repeatPlan
			   
    Plan handleRequest resumeLastPlan 
    [
    	println("[qcontrol] handling request");
		onMsg dataRequest : dataRequest(DOCTOR_ID, PATIENT_ID) -> 
			addRule request(PATIENT_ID);
		[!? response(PATIENT_ID, NAME, SURNAME, BLOOD_GROUP)]
			replyToCaller -m dataResponse : dataResponse(PATIENT_ID, NAME, SURNAME, BLOOD_GROUP)
    ]
}
