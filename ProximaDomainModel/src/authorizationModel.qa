System proxima

Dispatch identificationRequest : identificationRequest
Dispatch identificationResponse : identificationResponse(ID)
Dispatch dataRequest : dataRequest(RESCUER_ID, PATIENT_ID)
Dispatch dataResponse : dataResponse(RESCUER_ID, PATIENT_ID, NAME, SURNAME, BLOOD_GROUP)
Dispatch unauthorized : unauthorized

Event local_start : start

Context ctxAuthDebug ip[host="localhost" port=1406 ]

QActor qpatient context ctxAuthDebug {
	Rules {
		identifier(patient1).
	}
	
	Plan init normal
	[
		println("[qpatient] started!")
	]
	switchTo waitForRequest
	
	Plan waitForRequest 
	[
		println("[qpatient] waiting for request")
	]
	transition stopAfter 20000
			   whenMsg identificationRequest -> handleIdentificationRequest
			   finally repeatPlan
	
	Plan handleIdentificationRequest resumeLastPlan
	[
		println("[qpatient] handling identification request");
		[!? identifier(ID)] replyToCaller -m identificationResponse : identificationResponse(ID)
	]
}

QActor qpatient2 context ctxAuthDebug {
	Rules {
		identifier(patient2).
	}
	
	Plan init normal
	[
		println("[qpatient2] started!")
	]
	switchTo waitForRequest
	
	Plan waitForRequest 
	[
		println("[qpatient2] waiting for request")
	]
	transition stopAfter 20000
			   whenMsg identificationRequest -> handleIdentificationRequest
			   finally repeatPlan
	
	Plan handleIdentificationRequest resumeLastPlan
	[
		println("[qpatient2] handling identification request");
		[!? identifier(ID)] replyToCaller -m identificationResponse : identificationResponse(ID)
	]
}


QActor qrescuer context ctxAuthDebug {
	Rules {
		identifier(rescuer1).
		distance(qpatient, 100).
		distance(qpatient2, 150).
	}
	
	Plan init normal
	[
		println("[qrescuer] started!");
		demo consult("./src/it/unibo/qatheory/qRescuerTheory.pl");
		actorOp buildCustomGui("rescuerGui")
	]
	switchTo idle
	
	Plan idle 
	[
		println("[qrescuer] waiting for emergency")	
	]
	transition stopAfter 20000
			   whenEvent local_start -> requestPatientIdentifier
	
	Plan requestPatientIdentifier 
	[
		println("[qrescuer] sending identification request to patient");
		[!? findNearest(NAME, _)] forward NAME -m identificationRequest : identificationRequest
	]
	transition stopAfter 2000
			   whenMsg identificationResponse -> handlePatientIdentifierResponse
			   
    Plan handlePatientIdentifierResponse
    [
    	println("[qrescuer] patient identifier read");
    	onMsg identificationResponse : identificationResponse(PATIENT_ID) ->
			addRule patientid(PATIENT_ID)
    ] 
    switchTo sendDataRequest
    
    Plan sendDataRequest
    [
    	println("[qrescuer] sending data request to qdatacontrol");
    	[!? identifiers(RESCUER_ID, PATIENT_ID)] 
			forward qdatacontrol -m dataRequest : dataRequest(RESCUER_ID, PATIENT_ID)
    ]
    transition stopAfter 2000
    	whenMsg dataResponse -> handleDataResponse,
    	whenMsg unauthorized -> handleUnauthrorizedResponse
    
    Plan handleDataResponse
    [
    	println("[qrescuer] patient data retrieved");
    	onMsg dataResponse : dataResponse(RESCUER_ID, PATIENT_ID, NAME, SURNAME, BLOOD_GROUP) -> 
    		addRule dataResponse(RESCUER_ID, PATIENT_ID, NAME, SURNAME, BLOOD_GROUP)
    ] switchTo showPatientData
    
    Plan showPatientData
    [
    	println("[qrescuer] showing patient data");
    	[!? dataResponse(_, _, NAME, SURNAME, BLOOD_GROUP)] actorOp showDataOnGUI(NAME, SURNAME, BLOOD_GROUP)
    ] 
    switchTo idle
    
    Plan handleUnauthrorizedResponse 
    [
    	println("[qrescuer] unauthorized");
    	addRule unauthorized
    ]
    switchTo showUnauthorizedMessage
    
    Plan showUnauthorizedMessage
    [
    	println("[qrescuer] showing unauthorized message");
    	actorOp showUnauthorizedMessageOnGUI
    ]
    switchTo idle
}

QActor qdatacontrol context ctxAuthDebug {
	Rules {
		data(patient2, jane, doe, a_positive).
		data(patient1, john, doe, ab_negative).
		
		rescuer(rescuer1).
		rescuer(rescuer2).
	}
	
	Plan init normal 
	[
		demo consult("./src/it/unibo/qatheory/qDataControlAuthTheory.pl");
		println("[qdatacontrol] started!")
	]
	switchTo waitForRequest
	
	Plan waitForRequest
	[
		println("[qdatacontrol] waiting for request")
	]
	transition stopAfter 20000
			   whenMsg dataRequest -> handleRequest
			   finally repeatPlan
			   
    Plan handleRequest resumeLastPlan 
    [
		println("[qdatacontrol] handling data request");
		onMsg dataRequest : dataRequest(RESCUER_ID, PATIENT_ID) -> 
			addRule request(RESCUER_ID, PATIENT_ID);
		[!? authorized(RESCUER_ID, PATIENT_ID)]
			println("[qdatacontrol] rescuer authorized")
		else
			replyToCaller -m unauthorized : unauthorized;
		[!? response(RESCUER_ID, PATIENT_ID, NAME, SURNAME, BLOOD_GROUP)]
			replyToCaller -m dataResponse : dataResponse(RESCUER_ID, PATIENT_ID, NAME, SURNAME, BLOOD_GROUP);
		removeRule request(X, Y)
    ]
}
